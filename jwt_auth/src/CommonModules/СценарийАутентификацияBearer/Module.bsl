
Функция ВыполнитьСценарий(Знач КомпактJWT, Знач ВходАвторизации, Знач ПортыДоступа) Экспорт
	КомпактJWT = СокрЛП(КомпактJWT);
	Если СтрДлина(КомпактJWT) = 0 Тогда
		Доп = ОшибкиПриложения.ДопИнфоДоступа(ВходАвторизации, "Bearer");
		ОшибкиПриложения.НеАвторизован("Пустой JWT-сессия.", "API.Доступ.AUTHN.ПустойJWTCессия", Доп);
	КонецЕсли;
	
	Попытка
		ДанныеJWT = JWT_Логика.РазобратьJWT(КомпактJWT);
	Исключение
		Исх = ИнформацияОбОшибке();
		Доп = ОшибкиПриложения.ДопИнфоДоступа(ВходАвторизации, "Bearer");
		ОшибкиПриложения.НеАвторизован("Некорректный JWT.", "API.Доступ.AUTHN.НекорректныйJWT", Доп, Исх);
	КонецПопытки;
	
	Алг = УтилитыJSON.ПрочитатьСтроку(ДанныеJWT.Заголовок, "alg", Истина);
	Если Алг <> "RS256" Тогда
		Доп = ОшибкиПриложения.ДопИнфоДоступа(ВходАвторизации, "Bearer");
		ОшибкиПриложения.НеАвторизован("JWT alg должен быть RS256.", "API.Доступ.AUTHN.НеRS256", Доп);
	КонецЕсли;
	
	Утверждения = ДанныеJWT.Утверждения;
	Попытка
		ПроверитьОбязательнуюСтроку(Утверждения, "iss");
		ПроверитьОбязательныйAud(Утверждения);
		ПроверитьОбязательнуюСтроку(Утверждения, "sub");
		ПроверитьОбязательнуюСтроку(Утверждения, "jti");
		ПроверитьОбязательнуюСтроку(Утверждения, "uid");
		
		ИдентификаторТокена = ПреобразоватьUUIDСтрокуВУникальныйИдентификатор(Утверждения.jti, "jti");
		ИдентификаторПользователя = ПреобразоватьUUIDСтрокуВУникальныйИдентификатор(Утверждения.uid, "uid");
		
	Исключение
		Исх = ИнформацияОбОшибке();
		Доп = ОшибкиПриложения.ДопИнфоДоступа(ВходАвторизации, "Bearer");
		ОшибкиПриложения.НеАвторизован(
			"Некорректные claims JWT.",
			"API.Доступ.AUTHN.НекорректныеClaims",
			Доп,
			Исх
		);
	КонецПопытки;
	
	ХэшТокена = ПортыДоступа.Криптография.SHA256Hex(КомпактJWT);
	
	Сессия = ПортыДоступа.РегистрJWTСессий.СессияПоИдентификатору(ИдентификаторТокена, ПортыДоступа.Пользователи);
	Если Сессия = Неопределено Тогда
		Доп = ОшибкиПриложения.ДопИнфоДоступа(ВходАвторизации, "Bearer");
		Доп.Вставить("ИдентификаторПользователя", Строка(ИдентификаторПользователя));
		Доп.Вставить("ИдентификаторТокена", Строка(ИдентификаторТокена));
		ОшибкиПриложения.НеАвторизован("JWT-сессия не найдена.", "API.Доступ.AUTHN.НетJWTСессия", Доп);
	КонецЕсли;
	
	Если Сессия.Отозван Тогда
		Доп = ОшибкиПриложения.ДопИнфоДоступа(ВходАвторизации, "Bearer");
		ОшибкиПриложения.НеАвторизован("JWT-сессия отозвана.", "API.Доступ.AUTHN.ОтозванаJWTСессия", Доп);
	КонецЕсли;
	
	Если Сессия.ДействуетДо < ВходАвторизации.ТекущееВремяUTC Тогда
		Доп = ОшибкиПриложения.ДопИнфоДоступа(ВходАвторизации, "Bearer");
		ОшибкиПриложения.НеАвторизован("JWT-сессия истекла.", "API.Доступ.AUTHN.ИстеклаJWTСессия", Доп);
	КонецЕсли;
	
	Если Не УтилитыСравнения.СравнитьКонстантно(НРег(Сессия.ХэшТокена), НРег(ХэшТокена)) Тогда
		Доп = ОшибкиПриложения.ДопИнфоДоступа(ВходАвторизации, "Bearer");
		ОшибкиПриложения.НеАвторизован("JWT не соответствует allowlist-сессии.", "API.Доступ.AUTHN.НетAllowListJWTСессия", Доп);
	КонецЕсли;
	
	Если Сессия.Пользователь <> ИдентификаторПользователя Тогда
		Доп = ОшибкиПриложения.ДопИнфоДоступа(ВходАвторизации, "Bearer");
		ОшибкиПриложения.НеАвторизован("Пользователь JWT не соответствует сессии.", "API.Доступ.AUTHN.ПользовательНеJWTСессия", Доп);
	КонецЕсли;
	
	Субъект = Новый Структура;
	Субъект.Вставить("Схема", "Bearer");
	Субъект.Вставить("ИдентификаторПользователя", ИдентификаторПользователя);
	Субъект.Вставить("ИдентификаторТокена", ИдентификаторТокена);
	Субъект.Вставить("Утверждения", Утверждения);
	
	Возврат Субъект;
КонецФункции

Процедура ПроверитьОбязательнуюСтроку(Знач Утверждения, Знач ИмяПоля)
	Если ТипЗнч(Утверждения) <> Тип("Структура") Тогда
		ВызватьИсключение "Не авторизован: claims повреждены.";
	КонецЕсли;
	
	Значение = Неопределено;
	Если Не Утверждения.Свойство(ИмяПоля, Значение) Тогда
		ВызватьИсключение "Не авторизован: отсутствует обязательный claim '" + ИмяПоля + "'.";
	КонецЕсли;
	
	Если Значение = Неопределено Или ТипЗнч(Значение) <> Тип("Строка") Или СтрДлина(СокрЛП(Значение)) = 0 Тогда
		ВызватьИсключение "Не авторизован: claim '" + ИмяПоля + "' пустой или неверного типа.";
	КонецЕсли;
КонецПроцедуры

Процедура ПроверитьОбязательныйAud(Знач Утверждения)
	AudЗначение = Неопределено;
	Если Не Утверждения.Свойство("aud", AudЗначение) Тогда
		ВызватьИсключение "Не авторизован: отсутствует обязательный claim 'aud'.";
	КонецЕсли;
	
	Если AudЗначение = Неопределено Тогда
		ВызватьИсключение "Не авторизован: claim 'aud' пустой.";
	КонецЕсли;
	
	Если ТипЗнч(AudЗначение) = Тип("Строка") Тогда
		Если СтрДлина(СокрЛП(AudЗначение)) = 0 Тогда
			ВызватьИсключение "Не авторизован: claim 'aud' пустой.";
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(AudЗначение) = Тип("Массив") Или ТипЗнч(AudЗначение) = Тип("ФиксированныйМассив") Тогда
		Если AudЗначение.Количество() = 0 Тогда
			ВызватьИсключение "Не авторизован: claim 'aud' пустой массив.";
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	ВызватьИсключение "Не авторизован: claim 'aud' должен быть строкой или массивом строк.";
КонецПроцедуры

Функция ПреобразоватьUUIDСтрокуВУникальныйИдентификатор(Знач UUIDСтрока, Знач ИмяПоля)
	UUIDСтрока = СокрЛП(UUIDСтрока);
	
	Попытка
		Ид = Новый УникальныйИдентификатор(UUIDСтрока);
	Исключение
		ВызватьИсключение "Не авторизован: claim '" + ИмяПоля + "' не является UUID.";
	КонецПопытки;
	
	Возврат Ид;
КонецФункции
