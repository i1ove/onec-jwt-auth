
// Получить корреляцию из запроса или сгенерировать новую.
// Возвращает всегда НЕпустую строку UUID (lower-case).
Функция КорреляцияИзЗапроса(Знач Запрос) Экспорт
	Сырой = Заголовок(Запрос, "X-Correlation-Id", 64, "Обрезать");
	Норм = НормализоватьUUID(Сырой);
	
	Если СтрДлина(Норм) > 0 Тогда
		Возврат Норм;
	КонецЕсли;
	
	Возврат НРег(Строка(Новый УникальныйИдентификатор));
КонецФункции

// Безопасно получить заголовок. Возвращает строку ("" если заголовка нет).
// МаксДлина:
//   0  - без лимита
// РежимДлины:
//   "Ошибка"  - бросить исключение API.Данные.СлишкомДлинныйЗаголовок
//   "Обрезать" - обрезать до МаксДлина
Функция Заголовок(Знач Запрос, Знач ИмяЗаголовка, Знач МаксДлина = 0, Знач РежимДлины = "Ошибка") Экспорт
	Если Запрос = Неопределено Тогда
		ОшибкиПриложения.ОшибкаКонфигурации("Не задан объект Запрос.", "API.Конфигурация.ЗапросНеЗадан");
	КонецЕсли;
	
	Значение = Неопределено;
	
	Попытка
		Значение = Запрос.Заголовки.Получить(ИмяЗаголовка);
	Исключение
		ОшибкиПриложения.ОшибкаКонфигурации(
			"Не удалось прочитать заголовки HTTP-запроса.",
			"API.Инфраструктура.ЗаголовкиНедоступны"
		);
	КонецПопытки;
	
	Если Значение = Неопределено Тогда
		Возврат "";
	КонецЕсли;
	
	Стр = ОчиститьСтрокуДляЗаголовка(Значение);
	
	Если МаксДлина > 0 И СтрДлина(Стр) > МаксДлина Тогда
		Если РежимДлины = "Обрезать" Тогда
			Стр = Лев(Стр, МаксДлина);
		Иначе
			ОшибкиПриложения.ОшибкаДанных(
				"Слишком длинный заголовок " + ИмяЗаголовка + ".",
				"API.Данные.СлишкомДлинныйЗаголовок"
			);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Стр;
КонецФункции

// Нормализует UUID (36 символов, 8-4-4-4-12), возвращает lower-case.
// Возвращает "" если не UUID.
Функция НормализоватьUUID(Знач Значение) Экспорт
	Если ТипЗнч(Значение) <> Тип("Строка") Тогда
		Возврат "";
	КонецЕсли;
	
	Текст = СокрЛП(Значение);
	
	// защита от header/log injection
	Текст = СтрЗаменить(Текст, Символы.ПС, "");
	Текст = СтрЗаменить(Текст, Символы.ВК, "");
	Текст = СтрЗаменить(Текст, Символ(9), "");
	
	Если СтрДлина(Текст) <> 36 Тогда
		Возврат "";
	КонецЕсли;
	
	ШаблонUUID = "^[0-9A-Fa-f]{8}-[0-9A-Fa-f]{4}-[0-9A-Fa-f]{4}-[0-9A-Fa-f]{4}-[0-9A-Fa-f]{12}$";
	Если НЕ СтрПодобнаПоРегулярномуВыражению(Текст, ШаблонUUID) Тогда
		Возврат "";
	КонецЕсли;
	
	Возврат НРег(Текст);
КонецФункции

//////

Функция СвязатьИПроверить(Схема, ПараметрыЗапроса) Экспорт
	// Схема: Структура("СтрогийРежим,Поля", ..., Соответствие/СтруктураПолей)
	Ошибки = Новый Массив;
	Результат = Новый Структура;
	
	Поля = Схема.Поля;
	Строгий = Схема.СтрогийРежим;
	
	// 1) Строгий режим: неизвестные параметры запрещены
	Если Строгий Тогда
		Имена = ИменаПараметров(ПараметрыЗапроса);
		
		Для каждого ИмяПарам Из Имена Цикл
			Если Поля[ИмяПарам.Ключ] = Неопределено Тогда
				ДобавитьОшибку(Ошибки, ИмяПарам.Ключ, "UNKNOWN_PARAMETER", "Неизвестный query-параметр");
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	// 2) Привязка+валидация каждого известного поля
	Для каждого П Из Поля Цикл
		Имя = П.Ключ;
		Д = П.Значение; // дескриптор
		
		Сыр = ПолучитьПарамСтрокой(ПараметрыЗапроса, Имя);
		
		Если Сыр = Неопределено Или СтрДлина(СокрЛП(Сыр)) = 0 Тогда
			Если Д.Обязательный Тогда
				ДобавитьОшибку(Ошибки, Имя, "REQUIRED", "Параметр обязателен");
				Продолжить;
			Иначе
				Результат.Вставить(Имя, Д.ПоУмолчанию);
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		// Нормализация строки
		Сыр = СокрЛП(Сыр);
		
		Если Д.Тип = "Число" Тогда
			Попытка
				Ч = Число(Сыр);
			Исключение
				ДобавитьОшибку(Ошибки, Имя, "TYPE_NUMBER", "Ожидается число");
				Продолжить;
			КонецПопытки;
			
			Если Д.Свойство("Мин") И Ч < Д.Мин Тогда
				ДобавитьОшибку(Ошибки, Имя, "MIN", "Минимум: " + Строка(Д.Мин));
				Продолжить;
			КонецЕсли;
			
			Если Д.Свойство("Макс") И Ч > Д.Макс Тогда
				ДобавитьОшибку(Ошибки, Имя, "MAX", "Максимум: " + Строка(Д.Макс));
				Продолжить;
			КонецЕсли;
			
			Результат.Вставить(Имя, Ч);
			
		ИначеЕсли Д.Тип = "Строка" Тогда
			Если Д.Свойство("МаксДлина") И СтрДлина(Сыр) > Д.МаксДлина Тогда
				ДобавитьОшибку(Ошибки, Имя, "MAXLEN", "Макс. длина: " + Строка(Д.МаксДлина));
				Продолжить;
			КонецЕсли;
			
			Если Д.Свойство("НижнийРегистр") И Д.НижнийРегистр Тогда
				Сыр = ВНижний(Сыр);
			КонецЕсли;
			
			Если Д.Свойство("Допустимые") Тогда
				Если Д.Допустимые.Найти(Сыр) = Неопределено Тогда
					ДобавитьОшибку(Ошибки, Имя, "ENUM", "Недопустимое значение");
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			
			Если Д.Свойство("Шаблон") Тогда
				Если НЕ СовпадаетПоРегулярномуВыражению(Сыр, Д.Шаблон) Тогда
					ДобавитьОшибку(Ошибки, Имя, "PATTERN", "Некорректный формат");
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			
			Результат.Вставить(Имя, Сыр);
			
		Иначе
			ДобавитьОшибку(Ошибки, Имя, "SCHEMA", "Неизвестный тип поля в схеме");
		КонецЕсли;
		
	КонецЦикла;
	
	Если Ошибки.Количество() > 0 Тогда
		ОшибкиПриложения.ОшибкаВалидации("Некорректные параметры запроса", "HTTP.REQUEST.VALIDATION", Ошибки);
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

Функция ПолучитьПарамСтрокой(Параметры, Имя)
	// Универсально: поддержим и Структуру, и Соответствие
	Значение = Неопределено;
	
	Если Параметры = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если ТипЗнч(Параметры) = Тип("Структура")
		ИЛИ ТипЗнч(Параметры) = Тип("ФиксированнаяСтруктура") Тогда
		Если Параметры.Свойство(Имя, Значение) Тогда
			Возврат Строка(Значение);
		КонецЕсли;
	ИначеЕсли ТипЗнч(Параметры) = Тип("ФиксированноеСоответствие")
		Или ТипЗнч(Параметры) = Тип("Соответствие") Тогда
		Если Не Параметры.Получить(Имя) = Неопределено Тогда
			Возврат Строка(Параметры[Имя]);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Неопределено;
КонецФункции

Функция ИменаПараметров(Параметры)
	Рез = Новый Массив;
	
	Если Параметры = Неопределено Тогда
		Возврат Рез;
	КонецЕсли;
	
	Если ТипЗнч(Параметры) = Тип("Структура")
		или ТипЗнч(Параметры) = Тип("ФиксированнаяСтруктура") Тогда
		Для Каждого К Из Параметры Цикл
			Рез.Добавить(К.Ключ);
		КонецЦикла;
	ИначеЕсли ТипЗнч(Параметры) = Тип("ФиксированноеСоответствие")
		Или ТипЗнч(Параметры) = Тип("Соответствие") Тогда
		Для Каждого Ключ Из Параметры Цикл
			Рез.Добавить(Ключ);
		КонецЦикла;
	КонецЕсли;
	
	Возврат Рез;
КонецФункции

Функция ВНижний(Знач Стр)
	Если Стр = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат НРег(Стр);
КонецФункции

Процедура ДобавитьОшибку(Ошибки, Поле, Код, Сообщение)
	Ошибки.Добавить(Новый Структура("Поле,Код,Сообщение", Поле, Код, Сообщение));
КонецПроцедуры

Функция СовпадаетПоРегулярномуВыражению(Знач Текст, Знач Шаблон)
	// Требование: 8.3.23+ (иначе функции не будет)
	Возврат СтрПодобнаПоРегулярномуВыражению(Текст, Шаблон);
КонецФункции

//////
Функция ОчиститьСтрокуДляЗаголовка(Знач Значение)
	Если Значение = Неопределено Тогда
		Возврат "";
	КонецЕсли;
	
	Текст = Строка(Значение);
	
	// Убираем CR/LF/TAB (это важно и для логов, и для заголовков)
	Текст = СтрЗаменить(Текст, Символы.ПС, "");
	Текст = СтрЗаменить(Текст, Символы.ВК, "");
	Текст = СтрЗаменить(Текст, Символ(9), "");
	
	Возврат СокрЛП(Текст);
КонецФункции
