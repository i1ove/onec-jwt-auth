
// Сравнить “без раннего выхода”, чтобы по времени выполнения нельзя было угадать,
// на каком символе/байте случилось первое несовпадение
// классика против timing-attack’ов при сравнении секретов: HMAC/токены/ключи
Функция СравнитьКонстантно(Знач А, Знач Б) Экспорт
	Если А = Неопределено Тогда
		А = "";
	КонецЕсли;
	Если Б = Неопределено Тогда
		Б = "";
	КонецЕсли;

	Если ТипЗнч(А) = Тип("ДвоичныеДанные") Тогда
		ДД1 = А;
	Иначе
		Если ТипЗнч(А) <> Тип("Строка") Тогда
			А = Строка(А);
		КонецЕсли;
		ДД1 = ПолучитьДвоичныеДанныеИзСтроки(А, КодировкаТекста.UTF8, Ложь);
	КонецЕсли;

	Если ТипЗнч(Б) = Тип("ДвоичныеДанные") Тогда
		ДД2 = Б;
	Иначе
		Если ТипЗнч(Б) <> Тип("Строка") Тогда
			Б = Строка(Б);
		КонецЕсли;
		ДД2 = ПолучитьДвоичныеДанныеИзСтроки(Б, КодировкаТекста.UTF8, Ложь);
	КонецЕсли;

	Буфер1 = ПолучитьБуферДвоичныхДанныхИзДвоичныхДанных(ДД1);
	Буфер2 = ПолучитьБуферДвоичныхДанныхИзДвоичныхДанных(ДД2);

	Размер1 = Буфер1.Размер;
	Размер2 = Буфер2.Размер;
	Макс = ?(Размер1 > Размер2, Размер1, Размер2);

	Разница = ПобитовоеИсключительноеИли(Размер1, Размер2);

	Для i = 0 По Макс - 1 Цикл
		Б1 = 0;
		Б2 = 0;

		Если i < Размер1 Тогда
			Б1 = Буфер1[i];
		КонецЕсли;
		Если i < Размер2 Тогда
			Б2 = Буфер2[i];
		КонецЕсли;

		Разница = ПобитовоеИли(Разница, ПобитовоеИсключительноеИли(Б1, Б2));
	КонецЦикла;

	Возврат Разница = 0;
КонецФункции
