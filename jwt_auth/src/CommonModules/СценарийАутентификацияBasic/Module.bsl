
Функция ВыполнитьСценарий(Знач ЗначениеBase64, Знач ВходАвторизации, Знач ПортыДоступа) Экспорт
	ЗначениеBase64 = СокрЛП(Строка(ЗначениеBase64));
	Если СтрДлина(ЗначениеBase64) = 0 Тогда
		Доп = ОшибкиПриложения.ДопИнфоДоступа(ВходАвторизации, "Basic");
		ОшибкиПриложения.НеАвторизован("Пустой Basic.", "API.Доступ.AUTHN.ПустойBasic", Доп);
	КонецЕсли;

	Попытка
		Данные = Basic_Логика.РазобратьBasic(ЗначениеBase64);
	Исключение
		Исх = ИнформацияОбОшибке();
		Доп = ОшибкиПриложения.ДопИнфоДоступа(ВходАвторизации, "Basic");
		ОшибкиПриложения.НеАвторизован(
			"Некорректный Basic.",
			"API.Доступ.AUTHN.НекорректныйBasic",
			Доп,
			Исх
		);
	КонецПопытки;
	
	Логин = Данные.Логин;
	Пароль = Данные.Пароль;

	ПользовательИБ = ПортыДоступа.ПользователиИБ.НайтиПоЛогину(Логин);
	Если ПользовательИБ = Неопределено Тогда
		Доп = ОшибкиПриложения.ДопИнфоДоступа(ВходАвторизации, "Basic", , , Логин);
		ОшибкиПриложения.НеАвторизован("Пользователь не найден.", "API.Доступ.AUTHN.ПользовательНеНайден", Доп);
	КонецЕсли;

	СохрПароль = Неопределено;
	Если Не ПользовательИБ.Свойство("СохраняемоеЗначениеПароля", СохрПароль) Тогда
		Доп = ОшибкиПриложения.ДопИнфоДоступа(ВходАвторизации, "Basic");
		ОшибкиПриложения.ОшибкаКонфигурации("Не удалось получить пароль пользователя ИБ.", "API.Конфигурация.BasicНетПароляИБ", Доп);
	КонецЕсли;

	КлючСопоставления = Неопределено;
	Если Не ПользовательИБ.Свойство("КлючСопоставления", КлючСопоставления) Тогда
		Доп = ОшибкиПриложения.ДопИнфоДоступа(ВходАвторизации, "Basic");
		ОшибкиПриложения.ОшибкаКонфигурации("Не удалось получить ключ сопоставления ИБ.", "API.Конфигурация.BasicНетКлючаСопоставления", Доп);
	КонецЕсли;

	Попытка
		ХэшПароля = ПортыДоступа.Криптография.SHA1Base64(Пароль);
	Исключение
		Исх = ИнформацияОбОшибке();
		Доп = ОшибкиПриложения.ДопИнфоДоступа(ВходАвторизации, "Basic");
		ОшибкиПриложения.ОшибкаКонфигурации("Ошибка криптографии Basic.", "API.Конфигурация.КриптографияBasic", Доп, Исх);
	КонецПопытки;

	Если Не УтилитыСравнения.СравнитьКонстантно(Строка(СохрПароль), Строка(Пароль)) Тогда
		Доп = ОшибкиПриложения.ДопИнфоДоступа(ВходАвторизации, "Basic", , , Логин);
		ОшибкиПриложения.НеАвторизован("Неверный пароль.", "API.Доступ.AUTHN.НеверныйBasicПароль", Доп);
	КонецЕсли;

	ИдентификаторПользователя = ПортыДоступа.Пользователи.НайтиПоКлючуИБ(КлючСопоставления);
	Если ИдентификаторПользователя = Неопределено Тогда
		Доп = ОшибкиПриложения.ДопИнфоДоступа(ВходАвторизации, "Basic", , , Логин);
		ОшибкиПриложения.НеАвторизован("Доменный пользователь не найден.", "API.Доступ.AUTHN.ДоменныйПользовательНеНайден", Доп);
	КонецЕсли;

	Субъект = Новый Структура;
	Субъект.Вставить("Схема", "Basic");
	Субъект.Вставить("ИдентификаторПользователя", ИдентификаторПользователя);
	Субъект.Вставить("ИдентификаторТокена", Неопределено);
	Субъект.Вставить("Утверждения", Новый Структура(
		"login,match_key",
		Логин, Строка(КлючСопоставления)
	));

	Возврат Субъект;
КонецФункции
