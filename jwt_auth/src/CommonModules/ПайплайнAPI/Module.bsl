
Функция ВыполнитьПайплайн(Знач Запрос, Знач ПортыДоступа, Знач ПортыБизнеса, Знач Операция, Знач Исполнитель, Знач Презентер, Знач МапперВхода) Экспорт
	Корреляция = "";
	ВходАвторизации = Неопределено;
	КодУспехаHTTP = 200;
	
	Попытка
		
		Корреляция = HTTPПротоколAPI.КорреляцияИзЗапроса(Запрос);
		
		ВходАвторизации = Новый Структура;
		ВходАвторизации.Вставить("СтрокаАвторизации", HTTPПротоколAPI.Заголовок(Запрос, "Authorization", 4096, "Ошибка"));
		ВходАвторизации.Вставить("ТекущееВремяUTC", ПортыДоступа.Время.ТекущееВремяUTC());
		ВходАвторизации.Вставить("Корреляция", Корреляция);
		ВходАвторизации.Вставить("Транспорт", "HTTP");
		ВходАвторизации.Вставить("Операция", Операция);
		ВходАвторизации.Вставить("Клиент", HTTPПротоколAPI.Заголовок(Запрос, "User-Agent", 256, "Обрезать"));
		
		ТребуемыеОбласти = ПортыДоступа.ПолитикаОпераций.Области(Операция);
		
		КонтекстДоступа = КонтроллерДоступаAPI.ТребоватьКонтекст(
			ВходАвторизации,
			ТребуемыеОбласти,
			ПортыДоступа
		);
		
		ВходОперации = МапперВхода.СформироватьВход(Запрос);
		Результат = Исполнитель.ОбработатьЗапрос(ВходОперации, КонтекстДоступа, ПортыБизнеса);
		
		Представление = Презентер.ПредставитьУспех(Результат, КонтекстДоступа, Корреляция, КодУспехаHTTP);
		
		// Ожидаем структуру
		Если Представление = Неопределено Или ТипЗнч(Представление) <> Тип("Структура") Тогда
			ОшибкиПриложения.ОшибкаКонфигурации("Презентер вернул некорректное значение для операции " + Строка(Операция), "HTTP.PRESENTER.INCORRECT");
		КонецЕсли;
		
		Возврат HTTPОтветчикAPI.ОтветJSON(Представление.КодHTTP, Представление.Тело, Корреляция, Представление.Заголовки);
	
	Исключение
		Инфо = ИнформацияОбОшибке();
		
		КорреляцияЛок = Корреляция;
		Если СтрДлина(СокрЛП(КорреляцияЛок)) = 0 Тогда
			КорреляцияЛок = Строка(Новый УникальныйИдентификатор);
		КонецЕсли;
		
		М = HTTPМапперОшибокAPI.ИзИсключения(Инфо, ВходАвторизации, КорреляцияЛок);
		
		ЛогированиеAPI.ЗаписатьИсключениеВЖурналРегистрации(Инфо, КорреляцияЛок, ВходАвторизации, М.КодHTTP);
		
		Возврат HTTPОтветчикAPI.ОтветОшибки(М.КодHTTP, М.КодОшибки, М.Сообщение, КорреляцияЛок, М.Детали);
	КонецПопытки;
КонецФункции



