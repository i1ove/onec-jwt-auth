
Функция Аутентифицировать(Знач ВходАвторизации, Знач ПортыДоступа) Экспорт
	// Контракт ВходАвторизации валиден (проверен в КонтроллерДоступаAPI)
	СтрокаАвторизации = СокрЛП(ВходАвторизации.СтрокаАвторизации);
	Если СтрДлина(СтрокаАвторизации) = 0 Тогда
		Доп = ОшибкиПриложения.ДопИнфоДоступа(ВходАвторизации);
		ОшибкиПриложения.НеАвторизован(
			"Отсутствует заголовок Authorization.",
			"API.Доступ.AUTHN.ОтсутствуетAuthorization",
			Доп
		);
	КонецЕсли;
	
	Попытка
		Данные = ПарсерAuthorization.Разобрать(СтрокаАвторизации);
	Исключение
		Исх = ИнформацияОбОшибке();
		Доп = ОшибкиПриложения.ДопИнфоДоступа(ВходАвторизации);
			ОшибкиПриложения.НеАвторизован(
			"Некорректный заголовок Authorization.",
			"API.Доступ.AUTHN.НекорректныйAuthorization",
			Доп,
			Исх
		);
	КонецПопытки;

	Схема = Данные.Схема; // "Bearer"/"Basic"/"PAT"
	Значение = Данные.Значение; // compact jwt / base64 / pat.<uuid>.<secret>

	Если Схема = "Bearer" Тогда
		Возврат СценарийАутентификацияBearer.ВыполнитьСценарий(Значение, ВходАвторизации, ПортыДоступа);
	ИначеЕсли Схема = "PAT" Тогда
		Возврат СценарийАутентификацияPAT.ВыполнитьСценарий(Значение, ВходАвторизации, ПортыДоступа);
	ИначеЕсли Схема = "Basic" Тогда
		Возврат СценарийАутентификацияBasic.ВыполнитьСценарий(Значение, ВходАвторизации, ПортыДоступа);
	Иначе
		// Защита от будущих изменений парсера/контракта
		Доп = ОшибкиПриложения.ДопИнфоДоступа(ВходАвторизации);
		ОшибкиПриложения.ОшибкаКонфигурации(
			"Маршрутизатор: неподдерживаемая схема после парсинга: " + Строка(Схема),
			"API.Конфигурация.AUTHN.НеподдерживаемаяСхемаПослеПарсинга",
			Доп
		);
	КонецЕсли;
КонецФункции
