Процедура ЗаписатьСессию(Знач ИдентификаторТокена, Знач Пользователь, Знач ДействуетДоUTC, Знач ХэшТокена, Отозван) Экспорт
	Набор = СоздатьНаборЗаписей();
	Набор.Отбор.ИдентификаторТокена.Установить(ИдентификаторТокена);
	Набор.Прочитать();
	Набор.Очистить();

	Запись = Набор.Добавить();
	Запись.ИдентификаторТокена = ИдентификаторТокена;
	Запись.Пользователь = Пользователь;
	Запись.ДействуетДо = ДействуетДоUTC;
	Запись.Отозван = Отозван;
	Запись.ХэшТокена = ХэшТокена;

	Набор.Записать();
КонецПроцедуры

Функция СессияПоИдентификатору(ИдентификаторТокена) Экспорт
	
	Результат = Новый Структура("ИдентификаторТокена, Пользователь, ДействуетДо, ХэшТокена, Отозван");
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	JWTСессии.ИдентификаторТокена КАК ИдентификаторТокена,
		|	JWTСессии.Пользователь КАК Пользователь,
		|	JWTСессии.ДействуетДо КАК ДействуетДо,
		|	JWTСессии.ХэшТокена КАК ХэшТокена,
		|	JWTСессии.Отозван КАК Отозван
		|ИЗ
		|	РегистрСведений.JWTСессии КАК JWTСессии
		|ГДЕ
		|	JWTСессии.ИдентификаторТокена = &ИдентификаторТокена";
	
	Запрос.УстановитьПараметр("ИдентификаторТокена", ИдентификаторТокена);
	
	Выборка =  Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(Результат, Выборка);
	КонецЕсли;
	
	Возврат Результат;

КонецФункции